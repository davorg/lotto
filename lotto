#!/bin/env perl

use strict;
use warnings;
use 5.010;

my $config = parse_config();

my ($type, $count) = qw[lotto 1];

my @errs;
if (@ARGV == 2) {
  $type = shift;
  if (! exists $config->{$type}) {
    push @errs, qq["$type" is not a recognised type of lottery];
  }
}

if (@ARGV == 1) {
  $count = shift;
  if ($count !~ /^\d+$/) {
    push @errs, qq["$count" doesn't look like a positive integer];
  }
}

if (@ARGV || @errs) {
  push @errs, 'Usage: lotto [' . join('|', keys %$config) . "] [count]\n";
  die join "\n", @errs;
}

for (1 .. $count) {
  local $" = ', ';
  my @nums = lotto($config->{$type});
  say join ' : ', map { "@$_" } @nums;
}

sub lotto {
  my $lotto = shift;

  my @nums;
  foreach my $set (@$lotto) {
    my %tries;
    while (keys %tries < $set->{count}) {
      $tries{(int rand $set->{limit}) + 1}++;
    }
    push @nums, [ sort { $a <=> $b} keys %tries ];
  }

  return @nums;
}

sub parse_config {
  my $config;

  while (<DATA>) {
    chomp;
    my @conf = split /:/;
    my $key = shift @conf;
    foreach my $def (@conf) {
      my ($count, $limit) = split /x/, $def;
      push @{$config->{$key}}, { limit => $limit, count => $count };
    }
  }

  return $config;
}

__END__
euro:5x50:2x11
lotto:6x49
thunder:5x39:1x14
